plugins {
    id 'java'
    id 'maven-publish'
    id 'eclipse'
    id 'idea'
}


version = "3.0.0." + (System.getenv('BUILD_NUMBER') ?: 'CUSTOM')
group = "com.github.parker8283"
archivesBaseName = "BON"

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.ow2.asm:asm-debug-all:5.0.4'
    implementation 'com.google.guava:guava:18.0'
    implementation 'net.sf.jopt-simple:jopt-simple:4.9'
	
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.7'
	implementation group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'
	implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
}

task copyAndReplace(type: Copy) {
    from(sourceSets.main.java) {
        include '**/BON2.java'
        expand 'DEV': project.version
        filteringCharset = 'UTF-8'
    }
    from(sourceSets.main.java) {
        exclude '**/BON2.java'
    }
    into project.getBuildDir().toString() + "/sources/java"
    filteringCharset = 'UTF-8'
}
tasks.compileJava.dependsOn copyAndReplace
tasks.compileJava.setSource(project.getBuildDir().toString() + '/sources/java')

def commonManifest = {
    attributes 'Main-Class': 'com.github.parker8283.bon2.BON2',
            'Implementation-Title': 'Bearded Octo Nemesis 2',
            'Implementation-Version': project.version,
            'Built-With': 'Gradle v' + project.getGradle().getGradleVersion()
}

jar {
    manifest commonManifest
    archiveClassifier = 'bin'
}

task fatJar(type: Jar, dependsOn: 'jar') {
    inputs.file jar.archiveFile
    from(configurations.runtimeClasspath.collect { zipTree(it).matching { exclude 'META-INF/**' } })
    manifest commonManifest
    archiveClassifier = 'all'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

fatJar.doFirst {
    from zipTree(jar.archiveFile.get().asFile)
}

artifacts {
    archives fatJar
}
